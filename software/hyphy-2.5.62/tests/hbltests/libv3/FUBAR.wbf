GetString (version, HYPHY_VERSION, 0);

if (+version >= 2.4) {
    // overwrite the result file if its there
    fprintf (PATH_TO_CURRENT_BF + "data/CD2.nex.FUBAR.cache", CLEAR_FILE);

    LoadFunctionLibrary ("SelectionAnalyses/FUBAR.bf", {"--code" : "Universal", "--alignment" : PATH_TO_CURRENT_BF + "data/CD2.nex", "--branches" : "All"});
} else {
    return 0;

}
LoadFunctionLibrary ("shared.bf");

/*LoadFunctionLibrary ("libv3/IOFunctions.bf");
fscanf ("data/CD2.nex.FEL.json","Raw",json);
fel.json = Eval (json);
*/

/*
### Tabulating site-level results
|     Codon      |   Partition    |     alpha      |      beta      |Posterior prob for positive selection|
|:--------------:|:--------------:|:--------------:|:--------------:|:-----------------------------------:|
|       7        |       1        |        2.285   |       12.332   |       Pos. posterior = 0.9120       |
|       9        |       1        |        0.379   |        1.206   |       Pos. posterior = 0.9060       |
|       34       |       1        |        0.482   |        2.131   |       Pos. posterior = 0.9508       |
|       55       |       1        |        1.193   |        8.220   |       Pos. posterior = 0.9580       |
|       56       |       1        |        0.994   |        4.121   |       Pos. posterior = 0.9062       |
|       64       |       1        |        0.538   |        1.898   |       Pos. posterior = 0.9120       |
|       81       |       1        |        0.381   |        1.203   |       Pos. posterior = 0.9035       |
|      113       |       1        |        0.435   |        2.254   |       Pos. posterior = 0.9687       |
|      117       |       1        |        0.390   |        1.719   |       Pos. posterior = 0.9451       |
|      126       |       1        |        0.792   |        2.605   |       Pos. posterior = 0.9169       |
|      141       |       1        |        1.652   |        6.044   |       Pos. posterior = 0.9238       |
|      149       |       1        |        0.392   |        1.650   |       Pos. posterior = 0.9440       |
|      172       |       1        |        0.674   |        1.936   |       Pos. posterior = 0.9042       |
*/



assert (check_value (
   (+((fubar.json["grid"])[-1][2])[((fubar.json["grid"])[-1][0] - (fubar.json["grid"])[-1][1])["_MATRIX_ELEMENT_VALUE_<0"]]), 0.4922, 0.01), 
   "Incorrect weight on positively selected sites");
    

alphas     = (((fubar.json["MLE"])["content"])["0"])[-1][0];
betas      = (((fubar.json["MLE"])["content"])["0"])[-1][1];
pp         = (((fubar.json["MLE"])["content"])["0"])[-1][4];
bf         = (((fubar.json["MLE"])["content"])["0"])[-1][5];

test.bf_sum = 0;

test.expected_positives = utility.MatrixToDict({{7,9,34,55,56,64,81,113,117,126,141,149,172}});

function confirm_site (site, p, dict) {
    if (p >= 0.9) {
        if (dict/(site+1)) {
            dict - (site+1);
            return TRUE;
        } else {
            assert (0, "Failed to correctly classify site " + (site+1));
        }
    }
    if (dict/(site+1)) {
        assert (0, "Incorrectly classified site " + (site+1));
    }
    return FALSE;
}

utility.ForEachPair (pp,"_index_", "_p_",
                 "  
                   if (confirm_site (_index_[0], _p_, test.expected_positives)) {
                    test.bf_sum  += bf[_index_[0]];
                   }
                ");

assert (check_value (
    test.bf_sum, 195.21, 0.10), "More than 10% difference in cumulative BF for positively selected sites");
